plugins {
    id "java"
    id "com.athaydes.osgi-run" version "1.5.0"
    id "org.dm.bundle" version "0.8.4"
}

group 'osgi-run-tutorial'
version '1.0'

repositories {
    jcenter()
}

dependencies {
    compile "org.osgi:org.osgi.service.component.annotations:1.3.0"

    compile 'com.github.fedy2:yahoo-weather-java-api:2.0.0'

    osgiRuntime 'org.apache.felix:org.apache.felix.scr:2.0.2', {
        transitive = false
    }

}

runOsgi {
    bundles += project

    wrapInstructions {
        // use regex to match file name of dependency
        manifest( "yahoo-weather-java-api.*" ) {
            // export everything except the binding package and sub-packages
            instruction 'Export-Package', '!com.github.fedy2.weather.binding*', '*'
            instruction 'Bundle-Description', 'The Yahoo Weather Java API is used to provide weather data.'
        }
    }

}

bundle {
    instruction '-dsannotations', '*'
}

